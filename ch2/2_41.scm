(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
	  (accumulate op initial (cdr sequence)))))
(define (filter p seq)
  (cond ((null? seq)
	 seq)
	((p (car seq))
	 (cons (car seq) (filter p (cdr seq))))
	(else (filter p (cdr seq)))))
(define (enumerate-interval k n)
  (if (> k n)
      '()
      (cons k (enumerate-interval (add1 k) n))))
(define (flatmap proc seq)
  (accumulate append '() (map proc seq)))
(define (unique-pairs n)
  (flatmap
    (lambda (i)
      (map (lambda (j) (list i j))
	   (enumerate-interval 1 (- i 1))))
    (enumerate-interval 1 n)))
(define (unique-triples n)
  (flatmap
    (lambda (i)
      (map (lambda (pair)
	     (cons i pair))
	   (unique-pairs (- i 1))))
    (enumerate-interval 1 n)))
(define (triples-summing-to s)
  (filter (lambda (triple) (= s (apply + triple)))
	  (unique-triples (- s 3))))
